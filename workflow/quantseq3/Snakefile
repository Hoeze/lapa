
rule star_align_polyA:
    input:
        fq1 = config['quantseq']['fastq_raw'],
        index = config['star']['star_index']
    threads: 8
    resources:
        mem_gb = 32
    output:
        bam = config['quantseq']['bam']
    log:
        "logs/star/quantseq_{rep}.log"
    params:
        index = config['star']['star_index'],
        extra = "--outSAMtype BAM SortedByCoordinate --alignSJoverhangMin 10 --outSAMattributes All"
    wrapper:
        "0.72.0/bio/star/align"


rule count_quantseq_tailed:
    input:
        bam = config['quantseq']['bam']
    output:
        txt = 'data/results/quantseq/count_polyA/quantseq_{rep}.txt'
    threads: 1
    resources:
        mem_gb = 16
    script:
        "../common/lapa_count_tail.py"


rule count_quantseq:
    input:
        bam = config['quantseq']['bam']
    resources:
        mem_gb = 4
    output:
        txt = 'data/results/quantseq/count/quantseq_{rep}.txt'
    shell:
        "samtools view -F 4 -c {input.bam} > {output.txt}"


rule merge_count_quantseq:
    input:
        count_raw = expand(
            'data/results/quantseq/count/quantseq_{rep}.txt',
            rep=['1', '2']
        ),
        count_polyA = expand(
            'data/results/quantseq/count_polyA/quantseq_{rep}.txt',
            rep=['1', '2']
        )
    resources:
        mem_gb = 4
    output:
        csv = config['quantseq']['quantseq_count']
    script:
        "count.py"
